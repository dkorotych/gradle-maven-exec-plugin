/*
 * Copyright 2016 Dmitry Korotych.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import com.github.dkorotych.gradle.maven.exec.test.MavenTestHelperPlugin

plugins {
    id 'java-gradle-plugin'
    id 'idea'
    id 'jacoco'
    id 'net.researchgate.release' version '2.8.1'
    id "com.github.hierynomus.license" version "0.15.0"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'ru.vyarus.quality' version '4.8.0'
    id 'ru.vyarus.github-info' version '1.3.0'
    id 'org.openrewrite.rewrite' version '5.26.3'
    id "org.sonarqube" version "3.4.0.2513"
}

apply plugin: MavenTestHelperPlugin

group = 'com.github.dkorotych.gradle.maven.exec'
description = 'Gradle Maven Exec Plugin'

def versionPattern = /\d+(?:\.\d+)+/

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'com.google.code.bean-matchers:bean-matchers:0.14'
    testImplementation 'com.google.guava:guava:31.1-jre'

    rewrite("org.openrewrite:rewrite-gradle:7.27.3")
    rewrite("org.openrewrite.recipe:rewrite-github-actions:1.9.1")
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks:1.26.1")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks:1.10.1")
    rewrite("org.openrewrite.recipe:rewrite-migrate-java:1.9.1")
}

github {
    user 'dkorotych'
    license 'Apache'
}

gradlePlugin {
    plugins {
        MavenExecPlugin {
            id = "com.github.dkorotych.gradle.maven.exec"
            implementationClass = 'com.github.dkorotych.gradle.maven.exec.MavenExecPlugin'
            displayName = project.description
            description = 'Gradle plugin which provides an Maven exec task'
        }
    }
}

java {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    dependsOn(tasks.prepareTestEnvironments)
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
        if (System.getenv("CI")) {
            tag "GitHub"
            tag project.version
            publishAlways()
            uploadInBackground = false
            link "VCS", github.vcsUrl + "/tree/${System.getenv('GITHUB_REF_NAME')}"
        } else {
            tag "local"
        }
        tag System.getProperty("os.name")
    }
}

license {
    header project.file('gradle/config/license-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.developers = 'Dmitry Korotych'
    skipExistingHeaders true
    strictCheck true
    exclude "**/fixtures/**"
}

javadoc {
    options {
        links("https://docs.oracle.com/javase/${JavaVersion.toVersion(project.sourceCompatibility).majorVersion}/docs/api/",
                "https://docs.gradle.org/${gradle.gradleVersion}/javadoc/",
                "https://docs.groovy-lang.org/${GroovySystem.version}/html/gapi/")
    }
}

idea {
    project {
        jdkName = sourceCompatibility
        module {
            downloadJavadoc = false
            downloadSources = true
        }
    }
}

cleanIdea.doLast {
    delete file('.idea')
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "${github.user}_gradle-maven-exec-plugin"
        property "sonar.organization", github.user
        property "sonar.host.url", "https://sonarcloud.io"
        properties["sonar.test"] += 'buildSrc/src/main/java'
        properties["sonar.test"] += sourceSets.functionalTest.allSource.srcDirs
        properties["sonar.java.binaries"] += sourceSets.functionalTest.runtimeClasspath
    }
}

wrapper.gradleVersion = '6.0.1'

release {
    git {
        requireBranch = "(master|$versionPattern)"
    }
}

dependencyUpdates {
    checkForGradleUpdate = false
    revision = "release"
    resolutionStrategy {
        componentSelection {
            all { ComponentSelection selection ->
                def candidate = selection.candidate
                if (candidate.group == 'com.github.hierynomus.license'
                        && candidate.version >= '0.16.0') {
                    selection.reject("Plugin ${candidate.group} from version ${candidate.version} uses Gradle 7.0")
                }
                if (candidate.group == 'net.researchgate.release'
                        && candidate.version >= '3.0') {
                    selection.reject("Plugin ${candidate.group} from version ${candidate.version} uses Gradle 6.9")
                }
            }
        }
    }
}

updateVersion.doFirst {
    def file = file('README.md')
    def content = file.text
    content = content.replaceAll("id \"com.github.dkorotych.gradle-maven-exec\" version \"${versionPattern}\"",
            "id \"com.github.dkorotych.gradle-maven-exec\" version \"${version}\"")
    content = content.replaceAll("gradle-maven-exec-plugin:${versionPattern}",
            "gradle-maven-exec-plugin:${version}")
    file.text = content
}

rewrite {
    configFile = project.getRootProject().file("gradle/config/rewrite.yml")
    activeRecipe("com.github.dkorotych.gradle.maven.exec.MavenExecPlugin")
    exclusion("**/*.gradle")
}

//task generateMetricsPart {
//    def readmeFile = file('README.md')
//    def content = readmeFile.text
//    def header = '## Metrics'
//    def lastSpacesPattern = /\s+$/
//    def model = [
//            'header': header,
//            'template': 'FLAT',
//            'key': "${rootProject.group}:${rootProject.name}",
//            'metric': [
//                    'coverage',
//                    'bugs',
//                    'complexity',
//                    'code_smells',
//                    'vulnerabilities',
//                    'tests',
//                    'test_success_density',
//                    'violations',
//                    'ncloc',
//                    'lines'
//            ]
//    ]
//    def templateText = '''
//$header
//[![SonarQube Quality Gate](https://sonarqube.com/api/badges/gate?key=$key&template=$template)](https://sonarqube.com/dashboard?id=$key)
//<%
//metric.each {
//out.println("[![SonarQube ${it.capitalize().replaceAll('_', ' ')}](https://sonarqube.com/api/badges/measure?metric=${it}&key=$key&template=$template)](https://sonarqube.com/component_measures/metric/${it}/list?id=$key)")
//}
//%>
//'''.replaceAll(lastSpacesPattern, '')
//    def template = new SimpleTemplateEngine().createTemplate(templateText)
//    Writable metrics = template.make(model)
//    def metricsIndex = content.indexOf(header)
//    if (metricsIndex > 0) {
//        content = content.take(metricsIndex).replaceAll(lastSpacesPattern, '')
//    }
//    content += metrics
//    readmeFile.text = content
//}
