/*
 * Copyright 2016 Dmitry Korotych.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        if (JavaVersion.current().isJava8Compatible()) {
//            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
            if (gradle.gradleVersion < '6.0') {
                classpath 'ru.vyarus:gradle-quality-plugin:2.4.0'
            }
            classpath 'gradle.plugin.io.github.ddimtirov:codacy-gradle-plugin:0.1.0'
        }
    }
}

plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
    id 'net.researchgate.release' version '2.7.0'
    id "nebula.info" version "3.6.0"
    id "com.github.hierynomus.license" version "0.14.0"
}

group 'com.github.dkorotych.gradle.maven.exec'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'idea'
if (JavaVersion.current().isJava8Compatible()) {
//    apply plugin: "org.sonarqube"
    if (gradle.gradleVersion < '6.0') {
        apply plugin: 'ru.vyarus.quality'
    }
    apply plugin: 'io.github.ddimtirov.codacy'

    if (gradle.gradleVersion < '6.0') {
        quality {
            configDir = rootProject.file('config')
        }
    }

    jacoco {
        toolVersion = "0.8.1"
    }

    if (gradle.gradleVersion < '6.0') {
        afterEvaluate {
            codenarc {
                configFile = rootProject.file('config/codenarc/codenarc.groovy')
            }
        }
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

if (gradle.gradleVersion < '6.0') {
    dependencies {
        compile gradleApi()
        compile localGroovy()
        compile "commons-lang:commons-lang:2.6"
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
        testCompile 'cglib:cglib-nodep:3.2.4'
        testCompile 'org.objenesis:objenesis:2.4'
    }
} else {
    dependencies {
        implementation gradleApi()
        implementation localGroovy()
        implementation "commons-lang:commons-lang:2.6"
        testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
        testImplementation 'cglib:cglib-nodep:3.2.4'
        testImplementation 'org.objenesis:objenesis:2.4'
    }
}

license {
    header project.file('config/license-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.developers = 'Dmitry Korotych'
    skipExistingHeaders true
    strictCheck true
    exclude "**/fixtures/**"
}

pluginBundle {
    website = 'https://github.com/dkorotych/gradle-maven-exec-plugin'
    vcsUrl = 'https://github.com/dkorotych/gradle-maven-exec-plugin'
    description = 'Gradle plugin which provides an Maven exec task'
    tags = ['maven', 'exec', 'cross-platform']

    plugins {
        mavenExecPlugin {
            id = 'com.github.dkorotych.gradle-maven-exec'
            displayName = 'Gradle Maven Exec Plugin'
        }
    }
}

test {
    systemProperty("pluginVersion", project.version)
}

groovydoc {
    link 'http://docs.oracle.com/javase/7/docs/api/', 'java.', 'org.xml.', 'javax.', 'org.xml.'
    link "https://docs.gradle.org/${gradle.gradleVersion}/javadoc/", 'org.gradle'
    link "http://docs.groovy-lang.org/${GroovySystem.version}/html/gapi/",
            'groovy', 'org.apache.commons.cli', 'org.codehaus.groovy'
}

if (gradle.gradleVersion >= '4.10') {
    wrapper {
        gradleVersion = '2.13'
    }
} else {
    task wrapper(type: Wrapper) {
        gradleVersion = '2.13'
    }
}

idea {
    project {
        vcs = 'Git'
        module {
            downloadJavadoc = true
            downloadSources = true
        }
        ipr {
            withXml {
                def node = it.asNode()

                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

cleanIdea.doLast {
    delete file('.idea')
}

updateVersion.doFirst {
    def file = file('README.md')
    def content = file.text
    def versionPattern = /\d+(?:\.\d+)+/
    content = content.replaceAll("id \"com.github.dkorotych.gradle-maven-exec\" version \"${versionPattern}\"",
            "id \"com.github.dkorotych.gradle-maven-exec\" version \"${version}\"")
    content = content.replaceAll("gradle-maven-exec-plugin:${versionPattern}",
            "gradle-maven-exec-plugin:${version}")
    file.text = content
}

beforeReleaseBuild.dependsOn install

//task generateMetricsPart {
//    def readmeFile = file('README.md')
//    def content = readmeFile.text
//    def header = '## Metrics'
//    def lastSpacesPattern = /\s+$/
//    def model = [
//            'header': header,
//            'template': 'FLAT',
//            'key': "${rootProject.group}:${rootProject.name}",
//            'metric': [
//                    'coverage',
//                    'bugs',
//                    'complexity',
//                    'code_smells',
//                    'vulnerabilities',
//                    'tests',
//                    'test_success_density',
//                    'violations',
//                    'ncloc',
//                    'lines'
//            ]
//    ]
//    def templateText = '''
//$header
//[![SonarQube Quality Gate](https://sonarqube.com/api/badges/gate?key=$key&template=$template)](https://sonarqube.com/dashboard?id=$key)
//<%
//metric.each {
//out.println("[![SonarQube ${it.capitalize().replaceAll('_', ' ')}](https://sonarqube.com/api/badges/measure?metric=${it}&key=$key&template=$template)](https://sonarqube.com/component_measures/metric/${it}/list?id=$key)")
//}
//%>
//'''.replaceAll(lastSpacesPattern, '')
//    def template = new SimpleTemplateEngine().createTemplate(templateText)
//    Writable metrics = template.make(model)
//    def metricsIndex = content.indexOf(header)
//    if (metricsIndex > 0) {
//        content = content.take(metricsIndex).replaceAll(lastSpacesPattern, '')
//    }
//    content += metrics
//    readmeFile.text = content
//}

task additionalUseCaseTest(type: GradleBuild) {
    dependsOn 'install'
    group = 'verification'
    buildFile = file('useCase.gradle')
    tasks = ['checkMavenApplication3']
}

task realUseCaseTest(type: GradleBuild) {
    dependsOn 'install'
    group = 'verification'
    buildFile = file('useCase.gradle')
    def useCaseTasks = ['checkMavenApplication', 'checkMavenApplication2']
    useCaseTasks.addAll(additionalUseCaseTest.tasks)
    tasks = useCaseTasks
}

task kotlinUseCaseTest(type: GradleBuild) {
    dependsOn 'install'
    group = 'verification'
    buildFile = file('useCase.gradle.kts')
    tasks = ['checkMavenApplication', 'checkMavenApplication2', 'checkMavenApplication3']
}
